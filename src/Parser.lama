-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun toBinOp(lhs, op, rhs) {
  Binop (op, lhs, rhs)
}

-- Primary expression
var primary = memo $ eta syntax (
  x=decimal {Const (stringInt (x))} |
  x=lident  {Var (x)} |
  inbr[s("("), exp, s(")")]),
    exp = memo $ eta (
      expr(
        {
          [Left, {[s("&&") | s("!!"), toBinOp]}],
          [Left, {[s("==") | s("!=") | s("<") | s(">") | s("<=") | s(">="), toBinOp]}],
          [Left, {[s("+") | s("-"), toBinOp]}],
          [Left, {[s("*") | s("/") | s("%"), toBinOp]}]
        }, primary)
    );

var stmt = memo $ eta syntax (
  singleStmt |
  x=singleStmt s[";"] y=stmt {Seq(x, y)}),
singleStmt = memo $ eta syntax (
    kRead x=inbr[s("("), lident, s(")")] {Read (x)} |
    kWrite e=inbr[s("("), exp, s(")")] {Write (e)} |
    kSkip {Skip} |
    x=lident s[":="] e=exp {Assn(x, e)}
);

-- Public top-level parser
public parse = stmt;
